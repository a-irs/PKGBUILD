# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xrandr
pkgver=1.5.0
pkgrel=1
pkgdesc="Primitive command line interface to RandR extension"
arch=(i686 x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxrandr' 'libx11')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/xrandr-${pkgver}.tar.bz2{,.sig} no-bilinear.patch)
sha256sums=('c1cfd4e1d4d708c031d60801e527abc9b6d34b85f2ffa2cadd21f75ff38151cd'
            'SKIP'
            '3f44e92f108d26b6f46583a7640d073b91ad7528ca5e137c2fb93ea7327e1366')
validpgpkeys=('BD68A042C603DDAD9AA354B0F56ACC8F09BA9635') # Aaron Plattner <aplattner@nvidia.com>

prepare() {
    cd "${srcdir}/xrandr-$pkgver"
    patch -Np2 -i "${srcdir}/no-bilinear.patch"
}

build() {
  cd "${srcdir}/xrandr-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/xrandr-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
  rm -f "${pkgdir}/usr/bin/xkeystone"
}
